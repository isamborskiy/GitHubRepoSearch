apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.samborskii.githubreposearch"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        junitPlatform {
            filters {
                if (!project.hasProperty('runIntegrationTests')) {
                    excludeTags 'integration'
                }
            }
        }
    }
}

dependencies {
    implementation libraries.kotlin

    implementation libraries.appCompat
    implementation libraries.design
    implementation libraries.recyclerView
    implementation libraries.cardView

    implementation libraries.jackson
    implementation libraries.jacksonKotlin

    implementation libraries.okhttp
    implementation libraries.okhttpLogging
    implementation libraries.okhttpUrlConnection
    implementation libraries.retrofit
    implementation libraries.retrofitRxJava2
    implementation libraries.retrofitJackson

    implementation libraries.rxJava
    implementation libraries.rxKotlin
    implementation libraries.rxAndroid

    kapt libraries.daggerProcessor
    implementation libraries.dagger

    testImplementation libraries.junit4
    testImplementation libraries.junit5API
    testRuntimeOnly libraries.junit5Engine
    testRuntimeOnly libraries.junit5Vintage

    testImplementation libraries.mockwebserver

    androidTestImplementation libraries.testRunner
}
